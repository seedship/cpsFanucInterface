ROUTINE realToStr(input: REAL) : STRING
VAR
    buf : STRING[20]
BEGIN
    CNV_REAL_STR(input,8,8, buf)
    return (SUB_STR(buf, 2, 254))
END realToStr

ROUTINE connectServ(serverTag : STRING)
BEGIN
	MSG_DISCO(serverTag, STATUS)
	MSG_CONNECT(serverTag, STATUS)

	checkError('Connecting to Server', STATUS)

	-- Open File
	OPEN FILE connFile('RW', serverTag)

	STATUS = IO_STATUS(connFile)

	checkError('Opening File.', STATUS)
END connectServ

ROUTINE requestData(file_path : STRING) : INTEGER
VAR
	nBytes : INTEGER
	serverMsg : STRING[254]
	currentPos: XYZWPREXT
BEGIN
	$GROUP[1].$UTOOL = $MNUTOOL[1,1] --uTool 1
	$GROUP[1].$UFRAME = $MNUFRAME[1,1] --uFrame 1
	currentPos = CURPOS(0,0)
	WRITE connFile('GET /robot_position/?format=json')
	WRITE connFile('&x=', realToStr(currentPos.x))
	WRITE connFile('&y=', realToStr(currentPos.y))
	WRITE connFile('&z=', realToStr(currentPos.z))
	WRITE connFile('&w=', realToStr(currentPos.w))
	WRITE connFile('&p=', realToStr(currentPos.p))
	WRITE connFile('&r=', realToStr(currentPos.r), CR, CR)
	REPEAT
		BYTES_AHEAD(connFile, nBytes, STATUS)           
		IF STATUS <> 0 THEN
			RETURN (-1)
		ENDIF
		DELAY 100
	UNTIL nBytes > 0
	
	OPEN FILE pipeFile('RW', file_path)
	STATUS = IO_STATUS(pipeFile)
	checkError('Opening pipeFile', STATUS)

	WHILE nBytes > 0 DO
		READ connFile(serverMsg::nBytes)
		STATUS = IO_STATUS(connFile)

		checkError('I/O_Status (File)', STATUS)

		--wrapPrint(serverMsg)
		WRITE pipeFile(serverMsg)
		
		BYTES_AHEAD(connFile, nBytes, STATUS)           
		IF STATUS <> 0 THEN
			CLOSE FILE pipeFile
			RETURN (STATUS)
		ENDIF
		
	ENDWHILE
	CLOSE FILE pipeFile
	RETURN (0)
END requestData